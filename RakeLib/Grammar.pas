#	Comments & interleaves
rule(interleave=false) comment = "#" (. - ("\r" | "\n"))*;
interleave = (" " | "\r" | "\n" | "\t") | comment;

#	Primitives
rule(interleave=false, children=false) integer = ("0".."9")+;
rule(interleave=false) character = normal::(. - ("\"" | "\r" | "\n" | "\\"))
	| escapeQuote::("\\" "\"") | escapeBackslash::"\\\\"
	| escapeLetter:("\\" l::("n" | "r" | "t" | "v"))
	| escapeHexa:("\\x" h::("0".."9" | "a".."f" | "A".."F"){1,2});
rule(interleave=false) quotedString = "\"" s:character* "\"";

#	Identifiers
rule(interleave=false, children=false) letter = ("A".."Z") | ("a".."z");
rule(interleave=false, children=false) underscore = "_";
rule(interleave=false, children=false) identifier = (letter | underscore) (letter | underscore | integer)*;

#	Objects
rule parameterList = "(" h:expression t:("," e:expression)* ")";
rule methodWithParameters = e:expression "." m:identifier l:parameterList;
rule methodWithoutParameters = e:expression "." m:identifier "(" ")";
rule property = e:expression "." m:identifier;

#	Expression
rule(recursive=true) expression = integer
	| quotedString
	| identifier
	| property
	| methodWithoutParameters
	| methodWithParameters;

rule main = expression;